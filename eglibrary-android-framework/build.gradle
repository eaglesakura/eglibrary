apply plugin: 'com.android.library'
apply plugin: 'android-apt'

sourceCompatibility = 1.7
targetCompatibility = 1.7

def APPLICATION_ID = "com.eaglesakura.android.framework"

configurations {
    apt
}

def ANDROID_SUPPORT_LIB_VERSION = "23.0.+"
def ANDROID_PLAYSERVICE_LIB_VERSION = "7.8.+"

dependencies {
    compile "com.google.android.gms:play-services-plus:${ANDROID_PLAYSERVICE_LIB_VERSION}"
    compile "com.google.android.gms:play-services-gcm:${ANDROID_PLAYSERVICE_LIB_VERSION}"
    compile 'com.android.support:support-annotations:+'

    /**
     * support libs
     */
    compile "com.android.support:support-v4:${ANDROID_SUPPORT_LIB_VERSION}"
    compile "com.android.support:appcompat-v7:${ANDROID_SUPPORT_LIB_VERSION}"
    compile "com.android.support:cardview-v7:${ANDROID_SUPPORT_LIB_VERSION}"
    compile "com.android.support:recyclerview-v7:${ANDROID_SUPPORT_LIB_VERSION}"
    compile "com.android.support:palette-v7:${ANDROID_SUPPORT_LIB_VERSION}"
    compile "com.android.support:design:${ANDROID_SUPPORT_LIB_VERSION}"

    /**
     * floating action button
     */
    compile 'com.melnykov:floatingactionbutton:1.0.+'

    /**
     * annotations
     */
    apt "org.androidannotations:androidannotations:3.0.+"
    compile "org.androidannotations:androidannotations-api:3.0.+"
    compile "com.googlecode.android-query:android-query:+"

    compile project(':eglibrary-android-java-core')
}


android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 22
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java', 'src/main/dao', 'src/main/gen-prop', 'src/main/generated/dao']
        }
    }

    // ignore include
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }
}

apply plugin: 'maven'

if (!hasProperty("EGLIBRARY_ARTIFACT_VERSION")) {
    ext.EGLIBRARY_ARTIFACT_VERSION = ""
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

android.libraryVariants.all { variant ->
    if (!"release".equals(variant.name) || "".equals(EGLIBRARY_ARTIFACT_VERSION)) {
        return;
    }

    task("javadoc", type: Javadoc) {
        title = "${file(".").name} ver ${EGLIBRARY_ARTIFACT_VERSION}"
        description "Generates Android Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar) + files("build/intermediates/classes/${variant.name}/")
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        exclude '**/BuildConfig.java'
        exclude '**/*Dao.java' /* ignore greenDAO */
        exclude '**/DaoMaster.java' /* ignore greenDAO */
        exclude '**/DaoSession.java' /* ignore greenDAO */
        exclude '**/R.java' /* ignore greenDAO */
    }


    task("javadocJar", type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }
}
//task javadoc(type: Javadoc) {
//    title = "Android Release API"
//    description "Generates Javadoc for Release."
//    source = android.sourceSets.main.java.srcDirs
//    ext.androidJar =
//            "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
//    classpath = files(android.libraryVariants.release.javaCompile.classpath.files) + files(ext.androidJar) + files("build/intermediates/classes/release/")
//    options.links("http://docs.oracle.com/javase/7/docs/api/");
//    options.links("http://d.android.com/reference/");
//    exclude '**/BuildConfig.java'
//    exclude '**/*Dao.java'
//    exclude '**/R.java'
//}
//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}
//

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri(new File("${System.env.MAVEN_LOCAL_REPO}")))
            pom.version = EGLIBRARY_ARTIFACT_VERSION
            pom.groupId = "com.eaglesakura"
            pom.artifactId = file(".").absoluteFile.name;
        }
    }
}

[uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
    pom.project {
        inceptionYear '2011'
        packaging 'aar'
        licenses {
            license {
                name 'NYSL'
                url 'https://raw.githubusercontent.com/eaglesakura/eglibrary/master/LICENSE-NYSL-eng.txt'
            }
            license {
                name 'MIT License'
                url 'https://raw.githubusercontent.com/eaglesakura/eglibrary/master/LICENSE-MIT.txt'
            }
        }

        dependencies {
            dependency {
                groupId = 'com.eaglesakura'
                artifactId = 'eglibrary-android-java-wrapper'
                version = EGLIBRARY_ARTIFACT_VERSION
                scope = 'compile'
            }

            dependency {
                groupId = 'com.eaglesakura'
                artifactId = 'eglibrary-material-support'
                version = EGLIBRARY_ARTIFACT_VERSION
                scope = 'compile'
            }
        }
    }
}
apply from: "generator.gradle"