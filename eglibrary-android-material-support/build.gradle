apply plugin: 'com.android.library'
apply plugin: 'android-apt'

configurations {
    apt
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 22
    }

    lintOptions {
        disable 'MissingTranslation'
        disable "NewApi"
    }
}

dependencies {
    /**
     * support libs
     */
    compile 'com.android.support:support-v4:22.+'
    compile 'com.android.support:appcompat-v7:22.+'
    compile 'com.android.support:cardview-v7:22.+'
    compile 'com.android.support:recyclerview-v7:22.+'
//    compile 'com.android.support:support-v13:21.+'

    /**
     * floating action button
     */
    compile 'com.melnykov:floatingactionbutton:1.0.+'


    apt "org.androidannotations:androidannotations:3.0.+"
    compile "org.androidannotations:androidannotations-api:3.0.+"
    compile "com.googlecode.android-query:android-query:+"

    /**
     * lib
     */
    compile project(':eglibrary-android-java-core')
}


apply plugin: 'maven'

if (!hasProperty("EGLIBRARY_MAVEN_LOCAL_PATH")) {
    ext.EGLIBRARY_MAVEN_LOCAL_PATH = ""
}
if (!hasProperty("EGLIBRARY_ARTIFACT_VERSION")) {
    ext.EGLIBRARY_ARTIFACT_VERSION = ""
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

android.libraryVariants.all { variant ->
    if (!"release".equals(variant.name)) {
        return;
    }

    task("javadoc", type: Javadoc) {
        title = "$name $version API"
        description "Generates Android Javadoc for $variant.name."
        source = variant.javaCompile.source
        ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar) + files("build/intermediates/classes/${variant.name}/")
        options.links("http://docs.oracle.com/javase/7/docs/api/");
        options.links("http://d.android.com/reference/");
        exclude '**/BuildConfig.java'
        exclude '**/*Dao.java' /* ignore greenDAO */
        exclude '**/DaoMaster.java' /* ignore greenDAO */
        exclude '**/DaoSession.java' /* ignore greenDAO */
        exclude '**/R.java' /* ignore greenDAO */
    }


    task("javadocJar", type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }
}
//task javadoc(type: Javadoc) {
//    title = "Android Release API"
//    description "Generates Javadoc for Release."
//    source = android.sourceSets.main.java.srcDirs
//    ext.androidJar =
//            "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
//    classpath = files(android.libraryVariants.release.javaCompile.classpath.files) + files(ext.androidJar) + files("build/intermediates/classes/release/")
//    options.links("http://docs.oracle.com/javase/7/docs/api/");
//    options.links("http://d.android.com/reference/");
//    exclude '**/BuildConfig.java'
//    exclude '**/R.java'
//}


uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri(EGLIBRARY_MAVEN_LOCAL_PATH))
            pom.version = EGLIBRARY_ARTIFACT_VERSION
            pom.groupId = "com.eaglesakura"
            pom.artifactId = file(".").absoluteFile.name;
        }
    }
}

[uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
    pom.project {
        inceptionYear '2011'
        packaging 'aar'
        licenses {
            license {
                name 'NYSL'
                url 'https://raw.githubusercontent.com/eaglesakura/eglibrary/master/LICENSE-NYSL-eng.txt'
            }
            license {
                name 'MIT License'
                url 'https://raw.githubusercontent.com/eaglesakura/eglibrary/master/LICENSE-MIT.txt'
            }
        }

        dependencies {
            dependency {
                groupId = 'com.eaglesakura'
                artifactId = 'eglibrary-android-java-core'
                version = EGLIBRARY_ARTIFACT_VERSION
                scope = 'compile'
            }

            dependency {
                groupId = 'com.melnykov'
                artifactId = 'floatingactionbutton'
                version = "1.0.+"
                scope = 'compile'
            }


            dependency {
                groupId = 'com.googlecode.android-query'
                artifactId = 'android-query'
                version = '+'
                scope = 'compile'
            }

            dependency {
                groupId = 'org.androidannotations'
                artifactId = 'androidannotations-api'
                version = '3.0.+'
                scope = 'compile'
            }
        }
    }
}
