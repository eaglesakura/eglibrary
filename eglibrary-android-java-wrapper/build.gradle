buildscript {
    repositories {
        mavenCentral()

        maven {
            url "http://eaglesakura.github.io/maven/"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'android-apt'

if (!hasProperty("EGLIBRARY_MAVEN_LOCAL_PATH")) {
    EGLIBRARY_MAVEN_LOCAL_PATH = ""
}
if (!hasProperty("EGLIBRARY_ARTIFACT_VERSION")) {
    EGLIBRARY_ARTIFACT_VERSION = ""
}

configurations {
    apt
}

repositories {
    mavenCentral()
    maven {
        url "http://eaglesakura.github.io/maven/"
    }
}

dependencies {
    apt "org.androidannotations:androidannotations:3.0.+"
    compile "org.androidannotations:androidannotations-api:3.0.+"
    compile "com.googlecode.android-query:android-query:0.25.+"

    // greendao
    compile 'de.greenrobot:greendao:1.3.+'

    // protobuf
    compile 'com.google.protobuf:protobuf-java:2.5.+'

    compile fileTree(dir: 'libs', include: '*.jar')
    compile project(':eglibrary-java-wrapper')
    compile project(':eglibrary-android-java-core')
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java', 'src/main/dao']
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

buildscript {
    repositories {
        jcenter()

        mavenCentral()

        maven {
            url "http://eaglesakura.github.io/maven/"
        }
    }

    dependencies {
        classpath 'com.eaglesakura:eglibrary-gradle-plugin:0.1.+'
    }
}


apply plugin: 'com.eaglesakura.android-support'

/**
 * DBファイルを出力する
 */
task generateDao(type: com.eaglesakura.gradle.tasks.AndroidDaoGenTask) {
    outDirectory = file("src/main/dao").absoluteFile;
    classPackageBase = "com.eaglesakura.android.dao";

    def generateTkvs = {
        def schema = newSchema(0x01, "tkvs");
        def entity = schema.addEntity("DbKeyValueData");
        // uniqueId
        entity.addStringProperty("key").notNull().unique().primaryKey();
        // value
        entity.addStringProperty("value");
        // 更新日時
        entity.addDateProperty("date").notNull();
    }
    generateTkvs();

    def generateBkvs = {
        def schema = newSchema(0x01, "bkvs");
        def entity = schema.addEntity("DbKeyValueData");
        // uniqueId
        entity.addStringProperty("key").notNull().unique().primaryKey();
        // value
        entity.addByteArrayProperty("value");
        // 更新日時
        entity.addDateProperty("date").notNull();
    }
    generateBkvs();
}

apply plugin: 'maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri(EGLIBRARY_MAVEN_LOCAL_PATH))
            pom.version = EGLIBRARY_ARTIFACT_VERSION
            pom.groupId = "com.eaglesakura"
            pom.artifactId = file(".").absoluteFile.name;
        }
    }
}

[uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
    pom.project {
        inceptionYear '2011'
        packaging 'aar'
        licenses {
            license {
                name 'NYSL'
                url 'https://raw.github.com/eaglesakura/eglibrary/master/LICENSE'
            }
        }

        dependencies {
            dependency {
                groupId = 'com.eaglesakura'
                artifactId = 'eglibrary-java-wrapper'
                version = EGLIBRARY_ARTIFACT_VERSION
                scope = 'compile'
            }

            dependency {
                groupId = 'com.eaglesakura'
                artifactId = 'eglibrary-android-java-core'
                version = EGLIBRARY_ARTIFACT_VERSION
                scope = 'compile'
            }


            dependency {
                groupId = 'com.googlecode.android-query'
                artifactId = 'android-query'
                version = '0.25.+'
                scope = 'compile'
            }

            dependency {
                groupId = 'org.androidannotations'
                artifactId = 'androidannotations-api'
                version = '3.0.+'
                scope = 'compile'
            }

            dependency {
                groupId = 'de.greenrobot'
                artifactId = 'greendao'
                version = '1.3.+'
                scope = 'compile'
            }

            dependency {
                groupId = 'com.google.protobuf'
                artifactId = 'protobuf-java'
                version = '2.5.+'
                scope = 'compile'
            }
        }
    }
}
